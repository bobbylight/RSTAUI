plugins {
    id 'java-library'
}

['base', 'jacoco', 'distribution', 'maven-publish', 'signing'].each { apply plugin: it }

base {
    archivesName = 'rstaui'
}

dependencies {
    api 'com.fifesoft:rsyntaxtextarea:3.6.1'
    api 'com.fifesoft:autocomplete:3.3.3'
    testImplementation platform('org.junit:junit-bom:5.12.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.15.2'
}

ext.isReleaseVersion = !project.version.endsWith('SNAPSHOT')

jacocoTestReport {
    reports {
        xml.required = true // codecov depends on xml format report
        html.required = true
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}
jar {
	manifest {
        manifest {
            attributes('Class-Path': 'rsyntaxtextarea.jar autocomplete.jar',
                    'Specification-Title': 'RSTAUI',
                    'Specification-Version': version,
                    'Implementation-Title': 'org.fife.ui',
                    'Implementation-Version': version)

        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {

            groupId = 'com.fifesoft'
            artifactId = 'rstaui'
            version = version

            from components.java

            pom {
                name = 'rstaui'
                description = 'RSTAUI is an add-on library for RSyntaxTextArea that provides pre-build dialog boxes commonly needed in text editing applications.'
                url = 'https://bobbylight.github.io/RSyntaxTextArea/'
                inceptionYear = '2003'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://github.com/bobbylight/RSTAUI/blob/master/RSTAUI/src/main/dist/RSTAUI.License.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Robert Futrell'
                    }
                }
                scm {
                    url = 'https://github.com/bobbylight/RSTAUI'
                    connection = 'scm:git:git://github.com/bobbylight/RSTAUI'
                    developerConnection = 'scm:git:git@github.com:bobbylight/RSTAUI'
                    if (isReleaseVersion) {
                        tag = project.version
                    }
                }
            }
        }
    }
}

signing {
    // Don't require signing for e.g. ./gradlew install
    required = { gradle.taskGraph.hasTask('publish') && isReleaseVersion }
    sign publishing.publications.maven
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
