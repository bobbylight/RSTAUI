plugins {
    id 'com.github.spotbugs' version '5.0.13'
}

group 'com.fifesoft'
// NOTE: Local Java 8:  /Library/Java/JavaVirtualMachines/jdk1.8.0_191.jdk/Contents/Home

allprojects {

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    wrapper {
        gradleVersion = '7.6'
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    checkstyle {
        toolVersion = '9.3'
    }

    spotbugs {
        // Relative to subprojects
        includeFilter = file('../config/spotbugs-exclude.xml')
    }

    spotbugsMain {
        reports {
            html {
                required = true
            }
            xml {
                required = false
            }
        }
    }
    spotbugsTest {
        reports {
            html {
                required = true
            }
            xml {
                required = false
            }
        }
    }

    compileJava {
        sourceCompatibility javaVersion.toString()
        targetCompatibility javaVersion.toString()
        options.debug = true
        options.debugOptions.debugLevel = 'source,vars,lines'
        options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
    }
}
