['java', 'checkstyle', 'distribution', 'maven', 'signing'].each { apply plugin: it }

assert org.gradle.api.JavaVersion.current().isJava8Compatible()

group = 'com.fifesoft'
archivesBaseName = 'rstaui'

dependencies {
    compile files("${projectDir.absolutePath}/../RSyntaxTextArea/RSyntaxTextArea/build/classes/java/main")
    compile files("${projectDir.absolutePath}/../RSyntaxTextArea/RSyntaxTextArea/build/resources/main")
    compile files("${projectDir.absolutePath}/../AutoComplete/AutoComplete/build/classes/java/main")
    compile files("${projectDir.absolutePath}/../AutoComplete/AutoComplete/build/resources/main")
    testCompile 'junit:junit:4.12'
}

wrapper {
	gradleVersion = '5.5.1'
}

checkstyle {
    toolVersion = '8.14'
    configFile = 'config/checkstyle/checkstyle.xml' as File
}

compileJava {
	sourceCompatibility javaVersion
	targetCompatibility javaVersion
	options.debug = true
	options.debugOptions.debugLevel = 'source,vars,lines'
	options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

ext.sharedManifest = manifest {
	attributes('Class-Path': 'rsyntaxtextarea.jar autocomplete.jar',
		'Specification-Title': 'RSTAUI',
		'Specification-Version': version,
		'Implementation-Title': 'org.fife.ui',
		'Implementation-Version': version)

}
jar {
	manifest { from sharedManifest }
}

// We use "distributions" to create the zip files uploaded to SourceForge
distributions {
	main {
		baseName = 'rstaui'
		contents {
			from { [ 'build/libs' ] }
			rename 'RSTAUI-.*\\.jar', 'rstaui.jar'
		}
	}
	src {
		baseName = 'rstaui'
	}
}
distZip.classifier = null
distZip.dependsOn jar
srcDistZip.classifier = 'src'
srcDistZip {
	from projectDir
	include 'src/**/*'
	include 'build.gradle'
	include '.classpath'
	include '.project'
	include 'gradle.properties'
	include 'gradle/**/*'
	include 'gradlew*'
	include 'README.md'
	include '.settings/**'
}

task buildSourceForgeZips {
    doLast {
        println 'Building zip files for SourceForge'
    }
}
buildSourceForgeZips.dependsOn clean, jar, distZip, srcDistZip

// Stuff to generate and upload Maven artifacts
javadoc {
    source += files("${projectDir.absolutePath}/../RSyntaxTextArea/RSyntaxTextArea/src/main/java/**/*.java",
            "${projectDir.absolutePath}/../AutoComplete/AutoComplete/src/main/java/**/*.java")
	classpath += files("${projectDir.absolutePath}/../RSyntaxTextArea/RSyntaxTextArea/build/classes/main",
		"${projectDir.absolutePath}/../AutoComplete/AutoComplete/build/classes/main")
}
task javadocJar (type: Jar, dependsOn: javadoc) {
	manifest { from sharedManifest }
	classifier = 'javadoc'
	from javadoc.destinationDir
}
task sourceJar (type: Jar) {
	manifest { from sharedManifest }
	classifier = 'sources'
	from sourceSets.main.allSource
}
artifacts {
	archives jar, javadocJar, sourceJar
}
signing {
	// Don't require signing for e.g. ./gradlew install
	required { gradle.taskGraph.hasTask("uploadArchives") }
	sign configurations.archives
}
repositories {
	mavenCentral()
}
uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			if (project.hasProperty('upload') && Boolean.parseBoolean(upload)) { // gradlew uploadArchives -Pupload=true
				repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
				snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
			}
			else {
				repository(url: 'file:///' + projectDir + '/../localMavenRepo')
			}
			pom {
				groupId = 'com.fifesoft'
				name = 'rstaui'
				project {
					parent {
						groupId 'org.sonatype.oss'
						artifactId 'oss-parent'
						version '7'
						relativePath ''
					}
					
					groupId 'com.fifesoft'
					artifactId 'rstaui'
					packaging 'jar'
					
					name 'RSyntaxTextArea UI'
					description 'RSTAUI is an add-on library for RSyntaxTextArea that provides pre-build dialog boxes commonly needed in text editing applications.'
					
					inceptionYear '2003'
					url 'http://www.fifesoft.com/rsyntaxtextarea/'
					licenses {
						license {
							name 'Modified BSD License'
							url 'http://fifesoft.com/rsyntaxtextarea/RSyntaxTextArea.License.txt'
							distribution 'repo'
						}
					}
					
					scm {
						url 'https://github.com/bobbylight/RSTAUI'
						connection 'scm:git:git://github.com/bobbylight/RSTAUI'
						developerConnection 'scm:git:git@github.com:bobbylight/RSTAUI'
						if (!project.version.endsWith('-SNAPSHOT')) {
							tag project.version
						}
					}
					
					developers {
						developer {
							name 'Robert Futrell'
							url 'http://www.fifesoft.com'
							organization = 'Fifesoft' /* https://issues.gradle.org/browse/GRADLE-1200 */
							organizationUrl 'http://www.fifesoft.com/'
							roles {
								role 'architect'
								role 'developer'
							}
							timezone '0'
						}
					}
					
					// Manually add runtime dependencies since we don't have
					// a multi-project build set up properly
					dependencies {
						dependency {
							groupId 'com.fifesoft'
							artifactId 'rsyntaxtextarea'
							version project.version
						}
						dependency {
							groupId 'com.fifesoft'
							artifactId 'autocomplete'
							version project.version
						}
					}
					
					// Ugh, have to do this for the "build" section
					// http://stackoverflow.com/questions/27975786/trouble-injecting-the-build-block-while-exporting-a-maven-pom-xml-file-from-grad
					withXml {
						def pluginNode = asNode().appendNode('build').appendNode('plugins').appendNode('plugin')
						pluginNode.appendNode('artifactId', 'maven-compiler-plugin')
						pluginNode.appendNode('version', '3.5.1')
						def executionNode = pluginNode.appendNode('executions').appendNode('execution')
						executionNode.appendNode('id', 'default-compile')
						executionNode.appendNode('configuration').appendNode('source', javaVersion).
						      parent().appendNode('target', javaVersion)
					}
				}
			}
		}
	}
}
