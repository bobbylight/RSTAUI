plugins {
    id 'com.github.spotbugs' version '6.4.8'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

// We require building with JDK 17 or later. Built artifact compatibility
// is controlled by javaLanguageVersion
assert JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)

group = 'com.fifesoft'
// NOTE: Local Java 17: /Library/Java/JavaVirtualMachines/jdk-17.0.13+11/Contents/Home

allprojects {

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    wrapper {
        gradleVersion = '9.2.1'
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    test {
        useJUnitPlatform()
    }

    checkstyle {
        toolVersion = '12.3.0'
    }

    spotbugs {
        // Relative to subprojects
        includeFilter = file('../config/spotbugs-exclude.xml')
    }

    spotbugsMain {
        reports {
            html {
                required = true
            }
            xml {
                required = false
            }
        }
    }
    spotbugsTest {
        reports {
            html {
                required = true
            }
            xml {
                required = false
            }
        }
    }

    compileJava {
        options.release = Integer.parseInt(javaLanguageVersion)
        options.debug = true
        options.debugOptions.debugLevel = 'source,vars,lines'
        options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            // We must defensively check for these properties so CI builds work
            username.set(project.hasProperty('sonatypeUsername') ? sonatypeUsername : 'unknown')
            password.set(project.hasProperty('sonatypePassword') ? sonatypePassword : 'unknown')
        }
    }
}
